import org.ajoberstar.grgit.Grgit
import org.ajoberstar.gradle.git.release.opinion.Strategies

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
    }
}

plugins {
    id "org.ajoberstar.release-opinion" version "1.3.0"
    id "groovy"
    id "idea"
    id "eclipse"
    id "jacoco"
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination "$buildDir/jacocoHtml"
    }
}

apply from: "$rootDir/gradle/compilestatic.gradle"

repositories {
    jcenter()
}

dependencies {
    compile "io.reactivex:rxjava:1.0.4"
    compile "org.codehaus.groovy:groovy-all:2.3.9"
    compile "org.slf4j:slf4j-api:1.7.10"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:3.2.2"
    testCompile "ch.qos.logback:logback-core:1.1.3"
    testCompile "ch.qos.logback:logback-classic:1.1.3"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

release {
    // need to specify the repository to interact with
    grgit = Grgit.open(project.file("."))

    defaultVersionStrategy = Strategies.FINAL

    // the approach to creating tags can also be modified
    tagStrategy {
        prefixNameWithV = false // defaults to true
        generateMessage = { version -> "New version: $version.version" }
    }
}

apply from: "$rootDir/gradle/bintray.gradle"


wrapper {
    gradleVersion = "4.2.1"
}
